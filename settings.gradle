pluginManagement {
	repositories {
		maven { url = "https://maven.fabricmc.net/" }
		maven { url = "https://maven.architectury.dev/" }
		maven { url "https://maven.minecraftforge.net/" }
		gradlePluginPortal()
	}
}

def getMinecraftVersion() {
	String default_minecraft_version = "1.19.2"
	Properties localProperties = new Properties()
	String minecraft_version; int minecraft_version_int
	if (new File(rootDir, 'build.properties').exists()) {
		localProperties.load(new File(rootDir, 'build.properties').newDataInputStream())
	}
	if (localProperties.containsKey("MC_VERSION")) {
		String notDotted = localProperties.get("MC_VERSION")
		String dotted = notDotted.substring(0, 1) + "." +
				Integer.toString(Integer.parseInt(notDotted.substring(1, 3))) + "." +
				Integer.toString(Integer.parseInt(notDotted.substring(3, 5)))
		if (dotted.endsWith(".0")) {
			dotted = dotted.substring(0, dotted.length() - 2)
		}
		minecraft_version = dotted
	} else {
		minecraft_version = getProperties().containsKey("buildVersion") ? getProperties().get("buildVersion") : default_minecraft_version
	}
	minecraft_version_int = (minecraft_version.split("\\.")[0] +
			minecraft_version.split("\\.")[1].padLeft(2, '0') +
			(minecraft_version.split("\\.").length > 2 ? minecraft_version.split("\\.")[2].padLeft(2, '0') : "00"))
			.toInteger()

	return [minecraft_version, minecraft_version_int]
}

System.out.println("Minecraft version: " + getMinecraftVersion()[0])
int minecraft_version_int = getMinecraftVersion()[1]

include("common")
include("fabric")
if (minecraft_version_int < 12100) {
	include("forge")
} else {
	include("neoforge")
}

rootProject.name = "world-comment"
